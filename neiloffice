#!/usr/bin/env janet

(import mansion/buffet)
(import mansion/reception)
(import mansion/utils)

(import neil/psk :prefix "")
(import neil/hydrpc :as hr)

(def buffet-name "scores")

(defn start-reception
  []
  (:run (reception/open [buffet-name])))

(defn server
  "Starts RPC server"
  [reception &opt host port]
  (def [h p] (string/split ":" (or (os/getenv "NEIL_URL") "localhost:6660")))
  (default host h)
  (default port p)

  (def visitor (:visit reception "neil"))

  (defn save [what] (:save visitor buffet-name what))

  (defn retrieve [which] (:retrieve visitor buffet-name which))

  (defn load [id] (:load visitor buffet-name id))

  (defn stamp [&opt now]
    (default now (os/time))
    {:timestamp now})

  (defn populate [what data]
    (def now (os/time))
    (merge
      {:type (string what)
       :created now
       :state "active"}
      data
      (stamp now)))

  (defn- first-retrieved [what]
    (-> what retrieve first))

  (def funcs
    {:client/add (fn [_ client] (-> (populate :client client) freeze save))
     :client/list (fn [_] (first-retrieved {:type "client"}))
     :client/projects (fn [_ client] (first-retrieved {:client client}))
     :by-id (fn [_ id] (load id))
     :project/add (fn [_ project] (-> (populate :project project) freeze save))
     :project/list (fn [_] (first-retrieved {:type "project"}))
     :project/tasks (fn [_ project] (first-retrieved {:project project}))
     :task/add (fn [_ task] (-> (populate :task task) freeze save))
     :task/list (fn [_] (first-retrieved {:type "task"}))
     :task/by-state (fn [_ state] (utils/intersect (retrieve {:type "task" :state state})))
     :task/running (fn [self] (first (:task/by-state self "running")))
     :task/start (fn [_ id]
                   (def task (table ;(kvs (load id))))
                   (if-let [wi (task :work-intervals)]
                     (let [wi (array ;wi)]
                       (put task :work-intervals
                            (-> wi
                                (array/concat @[{:start (os/time)}])
                                freeze)))
                     (put task :work-intervals [{:start (os/time)}]))
                   (save [id (-> task
                                 (merge (stamp) {:state "running"})
                                 freeze)]))
     :task/stop (fn [_ note]
                  (def [[[id task]]] (retrieve {:state "running"}))
                  (def iw (array ;(task :work-intervals)))
                  (def running (array/pop iw))
                  (array/push iw
                              (merge running {:note note
                                              :end (os/time)}))
                  (save [id (-> task
                                (merge (stamp) {:state "active"
                                                :work-intervals iw})
                                freeze)]))
     :task/complete (fn [_ id]
                      (def nt
                        (-> id load
                            (merge (stamp) {:state "completed"})
                            freeze))
                      (save [id nt]))
     :task/cancel (fn [_ id]
                    (def nt
                      (-> id load
                          (merge (stamp) {:state "canceled"})
                          freeze))
                    (save [id nt]))})

  (print "Neil started to score on " host " " port)
  (hr/server funcs host port psk))

(defn main [_]
  (server (start-reception)))
